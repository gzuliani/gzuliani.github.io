<html>
<head>
<title>Pesca</title>
<link rel='stylesheet' type='text/css' href='../css/main.css'></link>
<link rel='stylesheet' type='text/css' href='css/sinclair.css'></link>
</head>
<body>
<p class="page-path"><a href="../index.html#sinclair">risorse</a> | <a href="index.html#pesca">zx spectrum</a> | pesca</p>
<h1>Pesca (1988)</h1>
<p><em>Pesca</em> &egrave; un programma realizzato per la gestione della pesca di beneficenza allestita durante i tradizionali festeggiamenti di ferragosto del mio paese. Ritoccato nel corso degli anni (l'ultima patch risale al 1997), ha fatto il suo dovere per ben 18 anni. Dal 2007 &egrave; stato sostituito da un ben meno pittoresco database Access.</p>
<p>Uno dei due ZX Spectrum in funzione nell'agosto del 2006:</p>
<p class="snapshot">
	<img class="snapshot" width="320" height="240" src="img/sagra1.jpg"/>
	<img class="snapshot" width="320" height="240" src="img/sagra2.jpg"/>
</p>
<p>Il programma, che fa uso di una procedura in linguaggio macchina per la visualizzazione dei caratteri a dimensioni maggiorate di cui non mi ricordo la provenienza, implementa una semplice ricerca binaria su due vettori numerici che contengono rispettivamente il numero di inizio e fine serie delle classi di premi previsti. Un terzo vettore alfanumerico contiene le descrizioni dei premi. Le matrici contenenti l'archivio sono salvate come file separati in coda al programma.</p>
<h2>Istantanea</h2>
<p class="snapshot"><img class="snapshot" width="320" height="240" src="img/pesca.png"/></p>
<h2>Nastro</h2>
<p>Scarica il nastro virtuale <a href="tzx/pesca.tzx">pesca.tzx</a> (23KB).</p>
<h2>Listato</h2>
<pre class="code">
 900 REM ******************
 910 REM *                *
1000 REM * GESTIONE PESCA *
1010 REM *                *
1020 REM ******************
1030 REM 
1040 REM 
1050 REM 
1060 CLEAR 59999: LET NMAX=5000
1070 LET GRUPPIMAX=300
1080 DIM I(GRUPPIMAX)
1085 DIM F(GRUPPIMAX)
1090 DIM D$(GRUPPIMAX,30)
1095 LET INIZIO=1: LET FINE=0
1098 POKE 23658,8
1099 PRINT &quot;CARICAMENTO L.M.  &quot;; FLASH 1;&quot;{-6}&quot;: LOAD &quot;&quot;CODE 60000
1100 REM 
1110 REM 
1120 REM 
1130 REM ***********
1140 REM *         *
1150 REM *  MENU'  *
1160 REM *         *
1170 REM ***********
1180 REM 
1190 REM 
1200 REM 
1210 BORDER 0: PAPER 0: INK 7: BRIGHT 1: CLS : POKE 23624,71
1220 PRINT AT 3,3;&quot;+-------------------------+&quot;
1230 PRINT AT 4,3;&quot;|                         |&quot;
1240 PRINT AT 5,3;&quot;|  1.. CREAZIONE ED       |     |      AGGIORNAMENTO      |&quot;
1250 PRINT AT 7,3;&quot;|                         |&quot;
1260 PRINT AT 8,3;&quot;|  2.. CARICAMENTO        |&quot;
1270 PRINT AT 9,3;&quot;|                         |&quot;
1280 PRINT AT 10,3;&quot;|  3.. MODIFICA           |&quot;
1290 PRINT AT 11,3;&quot;|                         |&quot;
1300 PRINT AT 12,3;&quot;|  4.. MODO RICERCA       |&quot;
1310 PRINT AT 13,3;&quot;|                         |&quot;
1320 PRINT AT 14,3;&quot;|  5.. FINE SESSIONE      |&quot;
1330 PRINT AT 15,3;&quot;|                         |&quot;
1340 PRINT AT 16,3;&quot;+-------------------------+&quot;
1350 PRINT AT 20,1;&quot;FAI LA TUA SCELTA  [1..5]&quot;
1360 LET A$=INKEY$
1370 IF A$=&quot;&quot; THEN GO TO 1360
1380 IF A$&lt;&quot;1&quot; OR A$&gt;&quot;5&quot; THEN GO TO 1360
1390 PRINT #0;AT 0,0;&quot;MI AUGURO SIA QUELLA GIUSTA....&quot;: FOR F=1 TO 100: NEXT F
1400 CLS : GO TO (VAL A$+1)*1000
1990 REM 
2000 REM #################
2010 REM #               #
2030 REM #  CREAZIONE E  #
2040 REM # AGGIORNAMENTO #
2050 REM #               #
2060 REM #################
2070 REM 
2080 PRINT INVERSE 1,,&quot;   CREAZIONE ED AGGIORNAMENTO   &quot;,,
2090 PRINT AT 5,0;&quot;VUOI &quot;; INVERSE 1;&quot;C&quot;; INVERSE 0;&quot;REARE OD &quot;; INVERSE 1;&quot;A&quot;; INVERSE 0;&quot;GGIORNARE ?&quot;
2100 LET A$=INKEY$
2110 IF A$=&quot;&quot; THEN GO TO 2100
2120 IF (A$=&quot;C&quot; OR A$=&quot;c&quot;) THEN LET IND=1: LET I(1)=1: GO TO 2270
2130 REM 
2140 REM 
2150 PRINT AT 7,0;&quot;* AGGIORNAMENTO ELENCO *&quot;
2151 PRINT AT 9,0;&quot;PROVENIENZA DELL'ELENCO:&quot;' INVERSE 1;&quot;1&quot;; INVERSE 0;&quot;-NASTRO&quot;' INVERSE 1;&quot;0&quot;; INVERSE 0;&quot;-MEMORIA&quot;
2153 LET A$=INKEY$
2155 IF A$=&quot;1&quot; THEN GO TO 2160
2157 IF A$=&quot;0&quot; THEN GO TO 2230
2159 GO TO 2153
2160 PRINT AT 9,0;&quot;INSERISCI IL NASTRO CONTENENTE&quot;'&quot;L'ELENCO  DA AMPLIARE  E PREMI&quot;'&quot;UN TASTO QUALUNQUE&quot;
2170 IF INKEY$&lt;&gt;&quot;&quot; THEN GO TO 2170
2180 IF INKEY$= &quot;&quot; THEN GO TO 2180
2190 PRINT FLASH 1;AT 13,0;&quot;CARICAMENTO ELENCO&quot;;AT 14,0;&quot;     IN CORSO     &quot;
2200 LOAD &quot;&quot; DATA I()
2205 LOAD &quot;&quot; DATA F()
2210 LOAD &quot;&quot; DATA D$()
2220 PRINT INVERSE 1;AT 13,0;&quot;CARICAMENTO ELENCO&quot;;AT 14,0;&quot;     ESEGUITO     &quot;
2230 LET IND=1: PRINT FLASH 1;AT 21,31;&quot;{-6}&quot;
2240 IF F(IND)&gt;0 THEN LET IND=IND+1: GO TO 2240
2245 PRINT AT 21,31;&quot; &quot;: LET I(IND)=F(IND-1)+1
2250 REM 
2260 REM 
2270 CLS : PRINT AT 0,0;&quot;GRUPPO N.  :&quot;;IND
2280 PRINT AT 3,0;&quot;INIZIO SERIE :&quot;;I(IND)
2290 PRINT AT 5,0;&quot;  FINE SERIE :&quot;;
2300 INPUT LINE F$
2310 IF LEN F$=0 THEN GO TO 2300
2320 LET X=1
2325 IF F$(X)&lt;&quot;0&quot; OR F$(X)&gt;&quot;9&quot; THEN GO TO 2300
2330 LET X=X+1: IF X&lt;=LEN F$ THEN GO TO 2325
2340 LET F=VAL F$
2345 IF F=0 THEN LET F(IND)=0: GO TO 2530
2350 IF F&gt;NMAX OR F&lt; I(IND) THEN GO TO 2300
2360 LET F(IND)=F
2380 LET I(IND+1)=F(IND)+1
2390 PRINT F
2400 PRINT AT 7,0;&quot;DESCRIZIONE ARTICOLO:&quot;;AT 9,0;
2405 DIM Z$(30)
2410 INPUT LINE Z$
2420 IF LEN Z$=0 THEN GO TO 2410
2440 PRINT Z$
2450 LET D$(IND)=Z$
2460 PRINT #0;AT 0,0;&quot;TUTTO A POSTO ?  [S/N]&quot;
2470 LET A$=INKEY$
2480 IF A$=&quot;&quot; THEN GO TO 2470
2486 IF CODE (A$)=13 THEN GO TO 2470
2490 IF A$=&quot;S&quot; OR A$=&quot;s&quot; THEN LET IND=IND+1
2500 GO TO 2270
2510 REM 
2520 REM 
2530 CLS : PRINT INVERSE 1,,&quot;   CREAZIONE ED AGGIORNAMENTO   &quot;,,
2540 PRINT AT 5,0;&quot;PRIMA DI USCIRE DA QUESTO&quot;'&quot;AMBIENTE, VUOI SALVARE IL&quot;'&quot;NUOVO ELENCO  [S/N]  ?&quot;
2550 LET A$=INKEY$
2560 IF A$=&quot;&quot; THEN GO TO 2550
2565 IF CODE A$=13 THEN GO TO 2550
2570 IF A$&lt;&gt;&quot;S&quot; AND A$&lt;&gt;&quot;s&quot; THEN GO TO 1200
2580 PRINT FLASH 1;AT 9,0; &quot; REGISTRAZIONE &quot;;AT 10,0;&quot;   IN  CORSO   &quot;
2590 SAVE &quot;ELENCO.I&quot; DATA I()
2600 SAVE &quot;ELENCO.F&quot; DATA F()
2610 SAVE &quot;ELENCO.D&quot; DATA D$()
2620 PRINT INVERSE 1;AT 9,0; &quot; REGISTRAZIONE &quot;;AT 10,0;&quot;   ESEGUITA    &quot;
2630 PRINT AT 12,0;&quot;VUOI FARE LA VERIFICA  [S/N] ?&quot;
2640 LET A$=INKEY$
2650 IF A$=&quot;&quot; THEN GO TO 2640
2660 IF CODE A$=13 THEN GO TO 2640
2670 IF A$&lt;&gt;&quot;S&quot; AND A$&lt;&gt;&quot;s&quot; THEN GO TO 1200
2770 PRINT AT 14,0;&quot;---&gt; PRONTO PER LA VERIFICA&quot;
2780 VERIFY &quot;&quot; DATA I()
2790 VERIFY &quot;&quot; DATA F()
2800 VERIFY &quot;&quot; DATA D$()
2810 PRINT AT 16,0;&quot;VERIFICA O.K.&quot;
2820 PAUSE 0: GO TO 1200
2990 REM 
3000 REM ###############
3010 REM #             #
3020 REM # CARICAMENTO #
3030 REM #             #
3040 REM ###############
3050 REM 
3060 REM 
3070 PRINT INVERSE 1,,&quot;     CARICAMENTO   ARCHIVIO     &quot;,,
3080 PRINT FLASH 1;AT 5,7;&quot;CARICAMENTO&quot;;AT 6,7;&quot; IN  CORSO &quot;
3090 PRINT AT 8,0;
3100 LOAD &quot;&quot; DATA I()
3110 LOAD &quot;&quot; DATA F()
3120 LOAD &quot;&quot; DATA D$()
3130 PRINT INVERSE 1;AT 5,7;&quot;CARICAMENTO&quot;;AT 6,7; &quot; ESEGUITO  &quot;
3140 PRINT #0;AT 0,0; FLASH 1;&quot;PREMI UN TASTO..&quot;
3150 PAUSE 0
3160 GO TO 1200
3170 REM 
4000 REM ############
4010 REM #          #
4020 REM # MODIFICA #
4030 REM #          #
4040 REM ############
4050 REM 
4055 LET PAGINA=1: LET UP=0: LET MOD=0
4060 CLS : PRINT &quot;N.  IN.  FINE    ARTICOLO&quot;
4065 PRINT &quot;--------------------------------&quot;
4067 PRINT 
4070 FOR R=1 TO 15
4080 LET IND=(PAGINA-1)*15+R
4085 IF F(IND)=0 THEN LET UP=1: GO TO 4110
4090 PRINT IND;TAB 4;I(IND);TAB 9;F(IND);TAB 14;D$(IND)( TO 15)
4100 NEXT R
4110 PRINT AT 20,0;&quot;A-AVANTI     E-MENU  M-MODIFICA&quot;'&quot;I-INSERISCI          C-CANCELLA&quot;
4120 LET A$=INKEY$
4130 IF A$=&quot;&quot; THEN GO TO 4120
4140 IF A$=&quot;E&quot; OR A$=&quot;e&quot; THEN GO TO 4900
4150 IF A$&lt;&gt;&quot;A&quot; AND A$&lt;&gt;&quot;a&quot; THEN GO TO 4180
4160 IF UP=1 THEN GO TO 4900
4170 LET PAGINA=PAGINA+1: GO TO 4060
4180 LET MOD=1: IF A$&lt;&gt;&quot;M&quot; AND A$&lt;&gt;&quot;m&quot; THEN GO TO 4450
4190 PRINT AT 20,0,,,,
4200 INPUT &quot;MODIFICA GRUPPO N. &quot;;G
4210 IF (G&lt;(PAGINA-1)*15+1) OR (G&gt; IND-UP) THEN GO TO 4200
4220 CLS : PRINT INVERSE 1;AT 0,0;&quot;          &quot;;AT 1,0;&quot; MODIFICA &quot;;AT 2,0;&quot;          &quot;
4225 PRINT AT 5,0;&quot;GRUPPO N.    : &quot;;G
4230 PRINT AT 7,0;&quot;INIZIO SERIE : &quot;;I(G)
4240 PRINT AT 9,0;&quot;  FINE SERIE : &quot;;
4250 INPUT LINE F$
4260 IF F$=&quot;&quot; THEN LET F= F(G): GO TO 4310
4270 LET F=VAL F$
4280 IF F &lt; I(G) THEN GO TO 4250
4290 IF F &gt; NMAX THEN GO TO 4250
4292 LET DIF=F-F(G)
4300 LET F(G)= F
4301 LET X=G+1
4304 IF F(X)&lt;&gt;0 THEN LET I(X)=I(X)+DIF: LET F(X)=F(X)+ DIF: LET X=X+1: GO TO 4304
4310 PRINT F
4320 PRINT AT 11,0;&quot;NOME PREMIO : &quot;
4330 INPUT LINE P$
4340 IF P$=&quot;&quot; THEN GO TO 4360
4350 LET D$(G)=P$
4360 PRINT 'D$(G)
4370 PRINT AT 20,0;&quot;TUTTO OK  [S/N]  ?&quot;
4380 LET A$=INKEY$
4390 IF A$=&quot;&quot; THEN GO TO 4380
4400 IF A$=&quot;N&quot; OR A$=&quot;n&quot; THEN GO TO 4220
4410 IF A$=&quot;S&quot; OR A$=&quot;s&quot; THEN GO TO 4060
4420 GO TO 4380
4430 REM 
4440 REM 
4450 IF A$&lt;&gt;&quot;C&quot; AND A$&lt;&gt;&quot;c&quot; THEN GO TO 4580
4460 INPUT &quot;GRUPPO DA CANCELLARE : &quot;;G
4470 IF (G&lt;(PAGINA-1)*15+1) OR (G&gt; IND-UP) THEN GO TO 4460
4480 LET DIF=F(G)-I(G)+1
4490 PRINT AT 0,31; FLASH 1;&quot;{-6}&quot;: LET X=G
4500 LET I(X)=I(X+1)-DIF
4510 LET F(X)=F(X+1)-DIF
4520 LET D$(X)=D$(X+1)
4530 LET X=X+1
4540 IF F(X+1)&lt;&gt;0 THEN GO TO 4500
4550 PRINT AT 0,31;&quot; &quot;: LET F(X)=0: GO TO 4060
4560 REM 
4570 REM 
4580 IF A$&lt;&gt;&quot;I&quot; AND A$&lt;&gt;&quot;i&quot; THEN GO TO 4060
4590 INPUT &quot;INSERIRE DOPO IL GRUPPO : &quot;;G
4600 IF (G&lt;(PAGINA-1)*15+1) OR (G&gt; IND-UP) THEN GO TO 4590
4610 PRINT AT 0,31; FLASH 1;&quot;{-6}&quot;: LET X=G
4620 IF F(X)&lt;&gt;0 THEN LET X=X+1: GO TO 4620
4630 IF X&gt;G THEN LET I(X)=I(X-1): LET F(X)=F(X-1): LET D$(X)=D$(X-1): LET X=X-1: GO TO 4630
4640 CLS : PRINT INVERSE 1;AT 0,0;&quot;             &quot;;AT 1,0;&quot; INSERIMENTO &quot;;AT 2,0;&quot;             &quot;
4650 PRINT ''&quot;GRUPPO N.    : &quot;;G+1
4660 LET I(G+1)=F(G)+1
4670 PRINT ''&quot;INIZIO SERIE : &quot;;I(G+1)
4680 PRINT ''&quot;  FINE SERIE : &quot;;
4690 INPUT F
4700 IF F &lt; I(G+1) OR F &gt; NMAX THEN GO TO 4690
4710 PRINT F: LET F(G+1)=F
4720 LET DIF=F(G+1)-I(G+1)+1
4730 PRINT AT 0,31; FLASH 1;&quot;{-6}&quot;: LET X=G+2
4740 IF F(X)&lt;&gt;0 THEN LET I(X)=I(X)+DIF: LET F(X)=F(X)+DIF: LET X=X+1: GO TO 4740
4790 PRINT AT 0,31;&quot; &quot;: PRINT AT 14,0;&quot;NOME PREMIO  : &quot;
4800 INPUT LINE D$(G+1): IF D$(G+1)=&quot;&quot; THEN GO TO 4800
4810 GO TO 4060
4900 IF MOD=0 THEN GO TO 1200
4910 CLS : PRINT &quot;ATTENZIONE!!&quot;
4920 PRINT &quot;------------&quot;''
4930 PRINT &quot;L'ELENCO E' STATO MODIFICATO.&quot;
4940 PRINT &quot;VUOI SCARICARLO SUL NASTRO [S/N]&quot;
4950 LET A$=INKEY$
4960 IF A$=&quot;&quot; THEN GO TO 4950
4970 IF A$=&quot;S&quot; OR A$=&quot;s&quot; THEN CLS : GO TO 2580
4975 GO TO 1200
4990 REM 
5000 REM ###########
5010 REM #         #
5030 REM # RICERCA #
5040 REM #         #
5050 REM ###########
5060 REM 
5061 PRINT &quot;{+6}{-2}{-4}{-3}{+7}{+6}{-6}{-5}{-3}{+7}{+4}{-3}&quot;
5062 PRINT &quot;{-6}{+7}{-5} {+5}{+5} {-5} {+5}{+5} &quot;
5063 PRINT &quot; {-5}{-5}{-3}{+5}{+5}{-7}{-5}{-3}{+7}{+4}{-2}&quot; 
5064 PRINT &quot;{-6}{+6}{-5} {+5}{-6}{+6}{-5} {+5}{+1}{+3}&quot;
5065 PRINT 
5066 PRINT &quot;{+6}{-2}{-4}{-3}{+7}{-3}{+4}{-2} {+5} {+4}{-6}{-5}{-5} {+5}{+6}{-2}&quot;
5067 PRINT &quot;{-6}{+7}{-5} {+5} {+5}  {+5} {+5}{-5}{-5}{-5}{-6}{+5}{-6}{+7}&quot;
5068 PRINT &quot; {-5}{-5} {+5} {+5}  {+5} {+4}{-2}{-5}{-5} {+5} {-5}&quot;
5069 PRINT &quot;{-6}{+6}{-1}{+3}{-2} {+5}  {+5} {+5} {-5}{-5} {+5}{-6}{+6}&quot;
5070 PRINT '&quot;BRESSA,  Agosto '97&quot;
5071 PLOT 175,95: DRAW 80,0: DRAW 0,80: DRAW -80,0: DRAW 0,-80
5072 PLOT 201,97: DRAW 52,0: DRAW 0,70: DRAW -52,-70: PLOT 177,119: DRAW 0,54: DRAW 68,0: DRAW -68,-54
5073 PLOT 177,117: DRAW 40,32: DRAW 8,4: DRAW -10,-8: DRAW 2,-1: DRAW 13,10: DRAW 15,11
5074 PLOT 177,97: DRAW 22,0: DRAW 29,40: DRAW 4,8: DRAW -8,-10: DRAW -1,2: DRAW 10,13: DRAW 12,15
5075 PLOT 209,99: DRAW 30,0: DRAW 12,60: DRAW -42,-60: PLOT 210,100: DRAW 28,0: DRAW 12,57: DRAW -39,-57
5076 PLOT 179,125: DRAW 0,30: DRAW 55,13: DRAW -55,-43: PLOT 180,126: DRAW 0,28: DRAW 53,14: DRAW -53,-41
5078 PRINT AT 14,0;&quot;BIGLIETTO&quot;
5079 PRINT AT 17,0;&quot;PREMIO :  &quot;
5080 PLOT 86,46: DRAW 67,0: DRAW 0,27: DRAW -67,0: DRAW 0,-27
5083 PLOT 6,6: DRAW 243,0: DRAW 0,20: DRAW -243,0: DRAW 0,-20
5084 PLOT 4,4: DRAW 247,0: DRAW 0,24: DRAW -247,0: DRAW 0,-24
5085 PRINT PAPER 7; INK 0;AT 13,11;&quot;        &quot;;AT 14,11;&quot;        &quot;;AT 15,11;&quot;        &quot;
5086 PRINT PAPER 0; INK 7; BRIGHT 1;AT 19,1;&quot;                              &quot;;AT 20,1;&quot;                              &quot;
5088 IF FINE&gt;0 THEN GO TO 5100
5089 LET FINE=1
5090 IF F(FINE+1)&gt;0 THEN LET FINE=FINE+1: GO TO 5090
5100 INPUT &quot;BIGLIETTO:&quot;;LINE B$
5110 IF LEN B$=0 THEN GO TO 5100
5120 LET X=1
5130 IF B$(X)&lt;&quot;0&quot; OR B$(X)&gt;&quot;9&quot; THEN GO TO 5100
5140 LET X=X+1: IF X&lt;=LEN B$ THEN GO TO 5130
5150 LET B=VAL B$
5160 IF B=0 THEN GO TO 1200
5170 IF B&gt;NMAX THEN GO TO 5100
5180 LET J=INIZIO: LET K=FINE
5190 LET Y=INT ((J+K)/2)
5200 IF B &gt; F(Y) THEN LET J=Y+1: GO TO 5190
5210 IF B &lt; I(Y) THEN LET K=Y-1: GO TO 5190
5211 POKE 60190,120: POKE 60194,104: POKE 60195,2: POKE 60196,3: POKE 60193,120-8*LEN B$
5212 FOR L=1 TO LEN B$
5214 POKE 60004,CODE (B$(L))
5218 RANDOMIZE USR 60000: NEXT L
5220 POKE 60190,71: POKE 60194,151: POKE 60195,1: POKE 60196,2: POKE 60193,8
5222 FOR L=1 TO 30
5224 POKE 60004,CODE D$(Y,L): RANDOMIZE USR 60000
5226 NEXT L
5230 PAUSE 0
5240 GO TO 5085
5920 REM 
5930 REM ##########
5940 REM #        #
5950 REM # USCITA #
5960 REM #        #
5970 REM ##########
5980 REM 
5990 REM 
6000 CLS : PRINT &quot;ORA SEI IN &quot;; INVERSE 1;&quot;BASIC&quot;; INVERSE 0;&quot;.&quot;'&quot;SE VUOI RITORNARE AL PROGRAMMA&quot;'&quot;NON DARE  &lt;RUN&gt;  MA  &lt;GOTO 1200&gt; &quot;
6010 STOP 
</pre>
<p class="modification-notice">Pagina modificata l'8/11/2011</p>
</body>
</html>
