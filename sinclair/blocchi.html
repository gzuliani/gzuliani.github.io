<html>
<head>
<title>Blocchi</title>
<link rel='stylesheet' type='text/css' href='../css/main.css'></link>
<link rel='stylesheet' type='text/css' href='css/sinclair.css'></link>
</head>
<body>
<p class="page-path"><a href="../index.html#sinclair">risorse</a> | <a href="index.html#blocchi">zx spectrum</a> | blocchi</p>
<h1>Blocchi (1987)</h1>
<p>Si tratta di un programma che dovrebbe dimostrare una certa abilit&agrave; nell'interpretazione del linguaggio naturale.</p>
<p>Il programma consente all'utente la manipolazione virtuale di un insieme di blocchi colorati. I blocchi si trovano in uno spazio bidimensionale, all'interno del quale possono essere impilati uno sull'altro oppure affiancati uno accanto all'altro. I blocchi sono rappresentati da una lettera che ne indica il colore: B=blu, G=giallo, R=rosso, V=verde. Durante l'esecuzione dei comandi impartiti, il programma descrive in maniera dettagliata le singole operazioni intraprese per raggiungere la configurazione desiderata.</p>
<h3>Esempio di conversazione</h3>
<p>Di seguito &egrave; riportato un esempio di utilizzo del programma:</p>
<ul>
	<li class="input">UTENTE: metti il cubo rosso sopra quello giallo</li>
	<li class="output">ZX SPECTRUM: eseguo! / cerco il blocco rosso&hellip; / preso / &egrave; gi&agrave; possibile muoverlo. / cerco il blocco giallo&hellip; / l'ho trovato / non vedo cubi sopra ad esso&hellip; / adesso sposto il blocco rosso sopra al blocco giallo</li>
	<li class="input">UTENTE: sposta il blocco giallo su quello blu</li>
	<li class="output">ZX SPECTRUM: eseguo! / cerco il blocco giallo&hellip; / preso / mi preparo a liberarlo&hellip; / cerco il blocco rosso&hellip; / preso / lo sto spostando&hellip; / cerco il blocco blu&hellip; / l'ho trovato / &hellip;&egrave; libero / ora sposto il blocco giallo sopra al blocco blu</li>
	<li class="input">UTENTE: cosa vedi?</li>
	<li class="output">ZX SPECTRUM: partendo da sinistra, il primo cubo che trovo &egrave; quello blu / sopra si trova il blocco giallo / quindi vedo il blocco rosso / infine scorgo il cubo verde / &egrave; tutto&hellip;</li>
	<li class="input">UTENTE: metti il blocco giallo a sinistra di quello rosso</li>
	<li class="output">ZX SPECTRUM: vediamo&hellip; / cerco il blocco giallo&hellip; / l'ho trovato / non vedo cubi sopra ad esso&hellip; / cerco il blocco blu&hellip; / trovato! / ora lo libero / cerco il blocco giallo&hellip; l'ho trovato / adesso lo sposto / -devo farlo per forza-</li>
	<li class="output">ZX SPECTRUM: ora affianco il cubo giallo a sinistra del cubo rosso</li>
</ul>
<h2>Istantanea</h2>
<p class="snapshot"><img class="snapshot" width="320" height="240" src="img/blocchi.png"/></p>
<h2>Nastro</h2>
<p>Scarica il nastro virtuale <a href="tzx/blocchi.tzx">blocchi.tzx</a> (16KB).</p>
<h2>Listato</h2>
<pre class="code">
  10 REM 
  20 REM ********************
  30 REM * INIZIALIZZAZIONE *
  40 REM ********************
  50 REM 
  60 DIM B$(20,20): DIM M$(5,6)
  70 FOR X=1 TO 5
  80 LET M$(X)=&quot;      &quot;
  90 NEXT X
 100 LET M$(1,2 TO 5)=&quot;BRVG&quot;
 110 LET LIMDES=6: LET LIMSIN=1
 120 LET FLAG=0: LET FL4=0: LET FL3=0: LET FL2=0: LET FL1=0
 130 BORDER 0: PAPER 0: INK 7: BRIGHT 1: CLS : POKE 23658,8: GO SUB 2710: GO SUB 160: GO TO 1560
 140 REM 
 150 REM *****************
 160 REM * DISEGNA MONDO *
 170 REM *****************
 180 REM 
 190 CLS : POKE 23624,71: PRINT '&quot;{+4}{-6}{-5}  {+6}{-6}{-4}{-3}{+7}{+6}{-6}{-5} {+5}{+5}&quot;'&quot;{+4}{-6}{-5}  {+5}{-5}{-5}  {+5} {-5}{+3}{+5}{+5}&quot;'&quot;{+1}{+6}{-5}{+3}{+7}{-6}{+6}{-1}{+3}{-2}{-6}{+6}{-5} {+5}{+5}&quot;''&quot;B=BLU    R=ROSSO&quot;'&quot;V=VERDE  G=GIALLO&quot;
 200 PRINT AT 1,23;&quot;########&quot;;: FOR F=5 TO 1 STEP -1:
 210 PRINT AT ABS (F-6)+1,23;&quot;#&quot;;: FOR N=1 TO 6: PRINT PAPER 7; INK 0;M$(F,N)+((CHR$ 8+&quot; &quot;) AND M$(F,N)=&quot;*&quot;);: NEXT N: PRINT &quot;#&quot;
 220 NEXT N: NEXT F: PRINT OVER 1;AT 7, 23;&quot;########&quot;
 230 RETURN 
 240 REM 
 250 REM ****************
 260 REM * CERCA BLOCCO *
 270 REM ****************
 280 REM 
 290 IF FL2 THEN PRINT &quot;CERCO IL BLOCCO &quot;;: LET X$=C$: GO SUB 2690: PRINT &quot;..&quot;
 300 LET L=1
 310 LET C=1
 320 IF M$(L,C)=C$ THEN GO TO 370
 330 LET C=C+1: IF C&lt;7 THEN GO TO 320
 340 LET L=L+1: IF L&lt;6 THEN GO TO 310
 350 IF RND&gt;.5 THEN PRINT &quot;NON L'HO TROVATO..&quot;: RETURN 
 360 PRINT &quot;QUESTO BLOCCO NON ESISTE.&quot;: RETURN 
 370 IF NOT FL2 THEN RETURN 
 380 LET A=RND: IF A&gt;.8 THEN PRINT &quot;ECCOLO!&quot;: RETURN 
 390 IF A&gt;.6 THEN PRINT &quot;L'HO TROVATO&quot;: RETURN 
 400 IF A&gt;.2 THEN PRINT &quot;TROVATO!&quot;: RETURN 
 410 PRINT &quot;PRESO&quot;: RETURN 
 420 REM 
 430 REM **********************
 440 REM * SPOSTAMENTO BLOCCO *
 450 REM **********************
 460 REM 
 470 LET FL2=FL3: GO SUB 260: LET FL2=0
 480 IF L=6 AND C=7 THEN RETURN 
 490 LET A=RND: IF A&gt;.75 THEN PRINT &quot;ORA &quot;;: GO TO 520
 500 IF A&gt;.5 THEN PRINT &quot;ADESSO &quot;;: GO TO 520
 510 IF A&gt;.25 THEN PRINT &quot;LO STO SPOSTANDO..&quot;: GO TO 530
 520 PRINT &quot;LO SPOSTO&quot;
 530 LET COL=1+(LIMSIN=2)+INT (RND*(6-(LIMDES=5)))
 540 IF NOT M$(1,COL)=&quot; &quot; THEN GO TO 590
 550 LET M$(1,COL)=M$(L,C): LET M$(L,C)=&quot; &quot;
 560 IF FL1 THEN PRINT &quot;-DEVO FARLO PER FORZA-&quot;
 570 PRINT #0;&quot;** PREMI UN TASTO **&quot;: PAUSE 0
 580 GO SUB 160: RETURN 
 590 LET COL=COL+1
 600 IF COL&gt;LIMDES THEN LET COL=LIMSIN
 610 GO TO 540
 620 REM 
 630 REM **********************
 640 REM * LIBERAMENTO BLOCCO *
 650 REM **********************
 660 REM 
 670 LET FL3=1: LET FL2=1: GO SUB 260: LET FL2=0
 680 IF L=6 AND C=7 THEN RETURN 
 690 IF M$(L+1,C)=&quot; &quot; THEN LET A=RND: PRINT (&quot;NON VEDO CUBI SOPRA AD ESSO..&quot; AND A&lt;=.33)+(&quot;..E' LIBERO&quot; AND A&gt;=.66)+(&quot;E' GIA' POSSIBILE MUOVERLO.&quot; AND A&gt;.33 AND A&lt;.66): LET FL3=0: RETURN 
 700 LET A=RND: IF A&gt;.75 THEN PRINT &quot;MI PREPARO A LIBERARLO..&quot;: GO TO 750
 710 IF A&gt;.5 THEN PRINT &quot;PRIMA DI TUTTO DEVO LIBERARLO&quot;: GO TO 750
 720 IF A&gt;.25 THEN PRINT &quot;ORA &quot;;: GO TO 740
 730 PRINT &quot;ADESSO &quot;;
 740 PRINT &quot;LO LIBERO..&quot;
 750 LET RIGA=5
 760 IF M$(RIGA,C)&lt;&gt;C$ AND M$(RIGA,C)&lt;&gt;&quot; &quot; THEN LET S$=C$: LET L1=L: LET C1=C: LET C$=M$(RIGA,C): GO SUB 440: LET C$=S$: LET L=L1: LET C=C1
 770 IF RIGA=L THEN LET FL3=0: RETURN 
 780 LET RIGA=RIGA-1
 790 GO TO 760
 800 REM 
 810 REM ********************
 820 REM * BLOCCO SOPRA A.. *
 830 REM ********************
 840 REM 
 850 LET C$=D$: GO SUB 260: LET CD=C: LET LD=L: LET C$=E$: GO SUB 260: LET LE=L: LET CE=C: IF CD=CE AND LE=LD+1 THEN PRINT &quot;SONO FORTUNATO!&quot;'&quot;NOTO CHE IL CUBO SOPRA AL BLOCCO&quot;: LET X$=D$: GO SUB 2690: PRINT &quot; E' PROPRIO QUELLO &quot;;: LET X$=E$: GO SUB 2690: PRINT #0;&quot;** PREMI UN TASTO **&quot;: PAUSE 0: GO SUB 160: RETURN 
 860 LET C$=E$
 870 GO SUB 640
 880 LET C$=D$
 890 GO SUB 640
 900 LET C$=E$: GO SUB 260
 910 LET L1=L: LET C1=C
 920 LET C$=D$: GO SUB 260
 930 PRINT (&quot;ORA&quot; AND A&gt;.5)+(&quot;ADESSO&quot; AND A&lt;=.5);&quot; SPOSTO IL BLOCCO &quot;;: LET X$=E$: GO SUB 2690: PRINT : PRINT &quot;SOPRA AL BLOCCO &quot;;: LET X$=D$: GO SUB 2690: PRINT : LET M$(L+1,C)=M$(L1,C1): LET M$(L1,C1)=&quot; &quot;: PRINT #0;&quot;** PREMI UN TASTO **&quot;: PAUSE 0: GO SUB 160
 940 RETURN 
 950 REM 
 960 REM ********************
 970 REM * BLOCCO SOTTO A.. *
 980 REM ********************
 990 REM 
1000 LET C$=E$: GO SUB 260: LET CE=C: LET LE=L: LET C$=D$: GO SUB 260: LET CD=C: LET LD=L
1010 IF CD=CE AND LE+1=LD THEN PRINT &quot;MA GUARDA CHE COMBINAZIONE!&quot;'&quot;IL CUBO &quot;;: LET X$=E$: GO SUB 2690: PRINT &quot; SI TROVA GIA'&quot;'&quot;SOTTO A QUELLO &quot;;: LET X$=D$: GO SUB 2690: PRINT ''&quot;NON E' CHE TU ABBIA&quot;'&quot;TENTATO DI FREGARMI !?&quot;: PRINT #0;&quot;** PREMI UN TASTO **&quot;: PAUSE 0: GO SUB 160: RETURN 
1020 LET C$=E$: GO SUB 640: LET C$=D$: GO SUB 640: LET CD=C: LET LD=L: LET C$=E$: GO SUB 260: LET CE=C: LET LE=L: LET C$=D$: LET FL3=1: GO SUB 440: LET FL3=0: GO SUB 260: LET LD1=L: LET CD1=C
1030 LET A=RND: PRINT ''(&quot;ADESSO &quot; AND A&gt;=.66)+(&quot;ORA &quot; AND A&lt;=.33)+(&quot;&quot; AND A&gt;.33 AND A&lt;.66);&quot;METTO IL BLOCCO &quot;;: LET X$=E$: GO SUB 2690: PRINT '&quot;DOVE HO VISTO IL CUBO &quot;;: LET X$=D$: GO SUB 2690: LET M$(LD,CD)=E$: LET M$(LE,CE)=&quot; &quot;: PRINT #0;&quot;** PREMI UN TASTO **&quot;: PAUSE 0: GO SUB 160
1040 LET A=RND: PRINT (&quot;INFINE&quot; AND A&gt;=.66)+(CHR$ 8 AND A&lt;=.33)+(&quot;FINALMENTE&quot; AND A&gt;.33 AND A&lt;.66);&quot; METTO IL CUBO &quot;;: LET X$=D$: GO SUB 2690: PRINT '&quot;SOPRA AL BLOCCO DI COLORE &quot;;: LET X$=E$: GO SUB 2690: LET M$(LD1,CD1)=&quot; &quot;: LET M$(LD+1,CD)=D$
1050 PRINT #0;&quot;** PREMI UN TASTO **&quot;: PAUSE 0: GO SUB 160: RETURN 
1060 REM ****************
1070 REM * ACCANTO AL.. *
1080 REM ****************
1090 REM 
1100 LET C$=D$: GO SUB 260: LET LD=L: LET CD=C: LET C$=E$: GO SUB 260: LET LE=L: LET CE=C: IF LD=LE AND CE=CD+DESTRA-NOT DESTRA THEN PRINT &quot;CHE COINCIDENZA!&quot;'&quot;IL BLOCCO &quot;;: LET X$=E$: GO SUB 2690: PRINT &quot; SI TROVA&quot;'&quot;PROPRIO ALLA &quot;+(&quot;DESTRA&quot; AND DESTRA)+(&quot;SINISTRA&quot; AND NOT DESTRA)'&quot;DEL CUBO &quot;;: LET X$=D$: GO SUB 2690: PRINT &quot;..&quot;: PRINT #0;&quot;** PREMI UN TASTO **&quot;: PAUSE 0: GO SUB 160: RETURN 
1110 LET C$=D$: GO SUB 260
1120 IF L&lt;&gt;1 OR ((C=6 AND DESTRA) OR (C=1 AND NOT DESTRA)) THEN LET LIMDES=6-DESTRA: LET LIMSIN=1+NOT DESTRA: GO SUB 640: GO SUB 440: LET LIMSIN=1: LET LIMDES=6
1130 GO SUB 260: LET L2=L: LET C2=C
1140 LET C$=E$: GO SUB 640
1150 IF NOT M$(L2,C2+DESTRA-NOT DESTRA)=&quot; &quot; THEN LET C$=M$(L2,C2+DESTRA-NOT DESTRA): GO SUB 640: LET FL1=1: GO SUB 440: LET FL1=0
1160 LET C$=E$: GO SUB 260
1170 LET A=RND: PRINT (&quot;FINALMENTE&quot; AND A&gt;.5)+(&quot;ORA&quot; AND A&lt;=.5);&quot; AFFIANCO IL CUBO &quot;': LET X$=E$: GO SUB 2690: PRINT &quot; A &quot;+(&quot;SINISTRA&quot; AND NOT DESTRA)+(&quot;DESTRA&quot; AND DESTRA)+&quot; DEL CUBO &quot;;: LET X$=D$: GO SUB 2690: LET M$(L2,C2+DESTRA-NOT DESTRA)=C$: LET M$(L,C)=&quot; &quot;: PRINT #0;&quot;** PREMI UN TASTO **&quot;: PAUSE 0: GO SUB 160
1180 RETURN 
1190 REM 
1200 REM ****************
1210 REM * ISOLA BLOCCO *
1220 REM ****************
1230 REM 
1240 GO SUB 260
1250 IF L&lt;&gt;1 THEN LET FL4=1: GO SUB 640: LET FL2=1: GO SUB 440
1260 IF C&gt;1 THEN IF M$(L,C-1)&lt;&gt;&quot; &quot; THEN GO TO 1300
1270 IF C&lt;6 THEN IF M$(L,C+1)&lt;&gt;&quot; &quot; THEN GO TO 1300
1280 IF M$(L+1,C)&lt;&gt;&quot; &quot; THEN GO TO 1300
1290 PRINT &quot;NON DEVO FARE NIENTE!&quot;'&quot;IL BLOCCO &quot;;: LET X$=C$: GO SUB 2690: PRINT &quot; E' GIA' ISOLATO&quot;: PRINT #0;&quot;** PREMI UN TASTO **&quot;: PAUSE 0: GO SUB 160: RETURN 
1300 IF C&gt;1 THEN IF  M$(L,C-1)=&quot; &quot; THEN LET M$(L,C-1)=&quot;*&quot;
1310 IF C&lt;6 THEN IF M$(L,C+1)=&quot; &quot; THEN LET M$(L,C+1)=&quot;*&quot;
1320 IF NOT FL4 THEN GO SUB 640
1330 LET FL4=0: LET K$=C$: IF C&gt;1 THEN LET S$=M$(L,C-1): IF S$=&quot;B&quot; OR S$=&quot;G&quot; OR S$=&quot;R&quot; OR S$=&quot;V&quot; THEN LET C$=S$: GO SUB 640: GO SUB 440: LET C$=K$: GO SUB 260: LET M$(L,C-1)=&quot;*&quot;
1340 IF C&lt;6 THEN LET D$=M$(L,C+1): IF D$=&quot;B&quot; OR D$=&quot;G&quot; OR D$=&quot;R&quot; OR D$=&quot;V&quot; THEN LET C$=D$: GO SUB 640: GO SUB 440: LET C$=K$: GO SUB 260
1350 IF C&gt;1 THEN IF M$(L,C-1)=&quot;*&quot; THEN LET M$(L,C-1)=&quot; &quot;
1360 IF C&lt;6 THEN IF M$(L,C+1)=&quot;*&quot; THEN LET M$(L,C+1)=&quot; &quot;
1370 RETURN 
1380 REM 
1390 REM *******************
1400 REM * MESCOLA BLOCCHI *
1410 REM *******************
1420 REM 
1430 PRINT &quot;RIMESCOLO I BLOCCHI..&quot;: FOR Y=1 TO 5
1440 FOR X=1 TO 6
1450 LET M$(Y,X)=&quot; &quot;
1460 NEXT X
1470 NEXT Y
1480 LET CB=1+INT (RND*6)
1490 LET CG=1+INT (RND*6): IF CG=CB THEN GO TO 1490
1500 LET CR=1+INT (RND*6): IF CR=CG OR CR=CB THEN GO TO 1500
1510 LET CV=1+INT (RND*6): IF CV=CR OR CV=CG OR CV=CB THEN GO TO 1510
1520 LET M$(1,CB)=&quot;B&quot;: LET M$(1,CG)=&quot;G&quot;: LET M$(1,CR)=&quot;R&quot;: LET M$(1,CV)=&quot;V&quot;: PRINT #0;&quot;** PREMI UN TASTO **&quot;: PAUSE 0
1530 GO SUB 160: RETURN 
1540 REM 
1550 REM ***********
1560 REM * COMANDO *
1570 REM ***********
1580 REM 
1590 LET FL3=0: INPUT &quot;COSA DEVO FARE, ORA ?&quot;'LINE A$: LET A$=A$+&quot; &quot;: LET PAROLA=1
1600 IF A$=&quot; &quot; THEN GO TO 1590
1610 LET B$(PAROLA)=&quot;                    &quot;: IF A$=&quot;&quot; THEN GO TO 1670
1620 IF A$(1)=&quot; &quot; THEN LET A$=A$(2 TO ): GO TO 1610
1630 FOR F=1 TO LEN A$: IF A$(F)=&quot; &quot; THEN GO TO 1660
1640 LET B$(PAROLA,F)=A$(F)
1650 NEXT F
1660 LET PAROLA=PAROLA+1: LET A$=A$(F TO ): IF PAROLA&lt;15 THEN GO TO 1610
1670 LET V=1
1680 IF B$(V, TO 3)=&quot;VAI&quot; OR B$(V, TO 3)=&quot;AND&quot; THEN PRINT #0;&quot;NON POSSO, VACCI TU!&quot;: PAUSE 250: GO TO 1560
1690 IF B$(V, TO 5)=&quot;SPOST&quot; OR B$(V, TO 4)=&quot;MUOV&quot; OR B$(V, TO 4)=&quot;METT&quot; OR B$(V, TO 8)=&quot;TRASPORT&quot; OR B$(V, TO 4)=&quot;PORT&quot; THEN GO TO 1860
1700 IF B$(V, TO 6)=&quot;MESCOL&quot; OR B$(V, TO 8)=&quot;RIMESCOL&quot; THEN GO TO 2030
1710 IF B$(V, TO 6)=&quot;RIPART&quot; OR B$(V, TO 9)=&quot;RICOMINCI&quot; THEN RUN 
1720 IF B$(V, TO 3)=&quot;VED&quot; OR B$(V, TO 7)=&quot;ILLUSTR&quot; OR B$(V, TO 5)=&quot;MOSTR&quot; OR B$(V, TO 7)=&quot;DESCRIV&quot; THEN GO TO 2120
1730 IF B$(V, TO 5)=&quot;ESIST&quot; OR B$(V, TO 4)=&quot;C'E'&quot; THEN GO TO 2480
1740 IF B$(V, TO 4)=&quot;ISOL&quot; OR B$(V, TO 5)=&quot;SEPAR&quot; THEN GO TO 2330
1750 IF B$(V, TO 2)=&quot;E'&quot; OR B$(V, TO 6)=&quot;DOV'E'&quot; OR B$(V, TO 4)=&quot;TROV&quot; OR B$(V, TO 2)=&quot;ST&quot; OR B$(V, TO 4)=&quot;CERC&quot; OR B$(V, TO 6)=&quot;RICERC&quot; THEN GO TO 2360
1760 IF B$(V, TO 7)=&quot;REGISTR&quot; OR B$(V, TO 5)=&quot;SALVA&quot; THEN PRINT &quot;O.K.&quot;'&quot;QUANDO SEI PRONTO PREMI UN TASTO&quot;: PAUSE 0: SAVE &quot;BLOCCHI&quot;LINE 10: PRINT ''&quot;VERIFICO LA REGISTRAZIONE..&quot;: VERIFY &quot;&quot;: GO SUB 160: GO TO 1560
1770 IF B$(V, TO 4)=&quot;FERM&quot; OR B$(V, TO 6)=&quot;NIENTE&quot; OR B$(V, TO 5)=&quot;NULLA&quot; OR B$(V, TO 5)=&quot;BASTA&quot; THEN PRINT &quot;COME VUOI..&quot;''&quot;PASSO IL CONTROLLO&quot;'&quot;AL SISTEMA OPERATIVO.&quot;'''&quot;CIAO, &quot;;H$: STOP 
1780 IF B$(V, TO 5)=&quot;SALUT&quot; OR B$(V, TO 4)=&quot;CIAO&quot; OR B$(V, TO 11)=&quot;ARRIVEDERCI&quot; THEN PRINT &quot;COSI' PRESTO ?&quot;'&quot;VA BENE, RESTITUISCO IL&quot;'&quot;CONTROLLO AL BASIC..&quot;''''&quot;E' STATO UN PIACERE POTER &quot;'&quot;LAVORARE CON TE!!&quot;''&quot;            CIAO, &quot;;H$: STOP 
1790 LET V=V+1: IF V&lt; PAROLA THEN GO TO 1680
1800 PRINT #0;AT 0,0;&quot;  NON CONOSCO IL VERBO CHE HAI&quot;'&quot;  USATO. PROVA CON UN SINONIMO&quot;: PAUSE 250: GO TO 1560
1810 REM 
1820 REM ******************
1830 REM * SPOSTA IL CUBO *
1840 REM ******************
1850 REM 
1860 LET Q=V: GO SUB 2570
1870 LET W=Q: GO SUB 2660: LET FLAG=1: LET A=RND
1880 LET E$=B$(W,1)
1890 GO SUB 2570: GO SUB 2660
1900 LET D$=B$(W,1): IF E$=D$ THEN LET A=RND: PRINT #0;AT 0,0;(&quot;MI STAI PRENDENDO IN GIRO ?&quot; AND A&gt;.5)+(&quot;HO SOLO UN CUBO DI COLORE &quot;+(E$+(&quot;LU&quot; AND E$=&quot;B&quot;)+(&quot;IALLO&quot; AND E$=&quot;G&quot;)+(&quot;OSSO&quot; AND E$=&quot;R&quot;)+(&quot;ERDE&quot; AND E$=&quot;V&quot;)) AND A&lt;=.5): PAUSE 250: GO TO 1560
1910 LET V=V+1
1920 IF B$(V, TO 3)=&quot;SU &quot; OR B$(V, TO 3)=&quot;SUL&quot; OR B$(V, TO 5)=&quot;SOPRA&quot; THEN PRINT (&quot;ESEGUO!&quot; AND A&lt;.33)+(&quot;CI PROVO&quot; AND A&gt;=.33 AND A&lt;=.66)+(&quot;BENE!&quot; AND A&gt;.66): GO SUB 850: GO TO 1560
1930 IF B$(V, TO 5)=&quot;SOTTO&quot; THEN PRINT (&quot;ESEGUO!&quot; AND A&lt;.33)+(&quot;CI PROVO&quot; AND A&gt;=.33 AND A&lt;=.66)+(&quot;BENE!&quot; AND A&gt;.66): GO SUB 970: GO TO 1560
1940 IF B$(V, TO 6)=&quot;DESTRA&quot; THEN LET DESTRA=1: PRINT (&quot;VA BENE&quot; AND A&gt;.5)+(&quot;O.K.&quot; AND A&lt;=.5): GO SUB 1100: GO TO 1560
1950 IF B$(V, TO 8)=&quot;SINISTRA&quot; THEN LET DESTRA=0: PRINT (&quot;COME VUOI..&quot; AND A&gt;.5)+(&quot;VEDIAMO..&quot; AND A&lt;=.5): GO SUB 1100: GO TO 1560
1960 LET V=V+1: IF V&lt; PAROLA THEN GO TO 1920
1970 PRINT #0;&quot;MI SPIACE, NON HO CAPITO DOVE&quot;'&quot;VUOI METTERE IL BLOCCO &quot;;E$+(&quot;LU&quot; AND E$=&quot;B&quot;)+(&quot;IALLO&quot; AND E$=&quot;G&quot;)+(&quot;OSSO&quot; AND E$=&quot;R&quot;)+(&quot;ERDE&quot; AND E$=&quot;V&quot;): PAUSE 250: GO TO 1560
1980 REM 
1990 REM *************
2000 REM * RIMESCOLA *
2010 REM *************
2020 REM 
2030 LET V=V+1
2040 IF B$(V, TO 7)=&quot;BLOCCHI&quot; OR B$(V, TO 4)=&quot;CUBI&quot; OR B$(V, TO 7)=&quot;OGGETTI&quot; THEN PRINT &quot;VA BENE..&quot;: GO SUB 1430: GO TO 1560
2050 LET V=V+1: IF V&lt; PAROLA THEN GO TO 2040
2060 PRINT #0;AT 0,0;&quot;  NON RIESCO A CAPIRE COSA TU&quot;'&quot;  MI VOGLIA FAR RIMESCOLARE..&quot;: PAUSE 250: GO TO 1560
2070 REM 
2080 REM ***********************
2090 REM * DESCRIVE SITUAZIONE *
2100 REM ***********************
2110 REM 
2120 LET V=V+1
2130 IF B$(V, TO 6)=&quot;BLOCCO&quot; OR B$(V, TO 4)=&quot;CUBO&quot; THEN GO TO 2390
2140 LET V=V+1: IF V&lt; PAROLA THEN GO TO 2130
2150 PRINT &quot;PARTENDO DA SINISTRA,IL PRIMO&quot;'&quot;CUBO CHE TROVO E' QUELLO &quot;;   
2160 LET BLOC=0: FOR V=1 TO 6: IF M$(1,V)=&quot; &quot; THEN NEXT V: GO TO 2470
2170 LET BLOC=BLOC +1
2180 LET A=RND: IF BLOC &gt;1 THEN PRINT (&quot;POI TROVO&quot; AND BLOC=2)+(&quot;QUINDI VEDO&quot; AND BLOC=3)+(&quot;INFINE SCORGO&quot; AND BLOC=4)+&quot; IL &quot;+(&quot;BLOCCO &quot; AND A&gt;.5)+(&quot;CUBO &quot; AND A&lt;=.5);
2190 LET X$=M$(1,V): GO SUB 2690: PRINT '
2200 IF M$(2,V)=&quot; &quot; THEN NEXT V: GO TO 2470
2210 PRINT &quot;SOPRA SI TROVA IL BLOCCO &quot;;: LET X$=M$(2,V): GO SUB 2690: PRINT : LET BLOC =BLOC+1
2220 IF M$(3,V)=&quot; &quot; THEN NEXT V: GO TO 2470
2230 PRINT &quot;PIU' SOPRA C'E' QUELLO &quot;;: LET BLOC=BLOC+1
2240 LET X$=M$(3,V): GO SUB 2690: PRINT 
2250 IF M$(4,V)=&quot; &quot; THEN NEXT V: GO TO 2470
2260 PRINT &quot;LA PILA TERMINA COL CUBO &quot;;: LET X$=M$(4,V): GO SUB 2690: PRINT '''&quot;TUTTI I BLOCCHI SONO&quot;'&quot;POSTI UNO SOPRA L'ALTRO!&quot;: GO TO 2470
2270 REM 
2280 REM ****************
2290 REM * ISOLA BLOCCO *
2300 REM ****************
2310 REM 
2320 LET V=V+1
2330 LET Q=V: GO SUB 2570: LET W=Q: GO SUB 2660: LET C$=B$(W,1): PRINT &quot;COME VUOI.&quot;: GO SUB 1210: GO TO 1560
2340 REM 
2350 REM ****************
2360 REM * CERCA BLOCCO *
2370 REM ****************
2380 REM 
2390 LET W=V: GO SUB 2660: LET C$=B$(W,1): LET FL2=0: GO SUB 260: LET FL2=1
2400 IF L=6 AND C=7 THEN GO TO 1560
2410 PRINT &quot;IL CUBO &quot;;: LET X$=C$: GO SUB 2690: PRINT &quot; SI TROVA SULLA &quot;'+(&quot;PRIMA&quot; AND C=1)+(&quot;SECONDA&quot; AND C=2)+(&quot;TERZA&quot; AND C=3)+(&quot;QUARTA&quot; AND C=4)+(&quot;QUINTA&quot; AND C=5)+(&quot;SESTA&quot; AND C=6)+&quot; COLONNA DA SINISTRA,&quot;
2420 PRINT (&quot;PRIMA&quot; AND L=1)+(&quot;SECONDA&quot; AND L=2)+(&quot;TERZA&quot; AND L=3)+(&quot;QUARTA&quot; AND L=4)+(&quot;QUINTA&quot; AND L=5)+&quot; LINEA PARTENDO DAL BASSO&quot;
2430 IF C&gt;1 THEN IF M$(L,C-1)&lt;&gt;&quot; &quot; THEN PRINT &quot;A SINISTRA C'E' IL CUBO &quot;;: LET X$=M$(L,C-1): GO SUB 2690: PRINT 
2440 IF C&lt;6 THEN IF M$(L,C+1)&lt;&gt;&quot; &quot; THEN PRINT &quot;A DESTRA VEDO IL CUBO &quot;;: LET X$=M$(L,C+1): GO SUB 2690: PRINT 
2450 IF M$(L+1,C)&lt;&gt;&quot; &quot; THEN PRINT &quot;SOPRA E' POSTO IL BLOCCO &quot;;: LET X$=M$(L+1,C): GO SUB 2690: PRINT 
2460 IF L&gt;1 THEN IF M$(L-1,C)&lt;&gt;&quot; &quot; THEN PRINT &quot;SOTTO SI TROVA IL CUBO &quot;;: LET X$=M$(L-1,C): GO SUB 2690: PRINT 
2470 PRINT : PRINT &quot;E' TUTTO..&quot;:: PRINT #0;&quot;** PREMI UN TASTO **&quot;: PAUSE 0: GO SUB 160: GO TO 1560
2480 LET Q=V: GO SUB 2570: LET W=Q: GO SUB 2660: LET A=RND
2490 PRINT (&quot;SI', EFFETTIVAMENTE ESISTE&quot; AND A&lt;.25)+(&quot;E' VERO, RIESCO A VEDERE&quot; AND A&gt;=.25 AND A&lt;.5)+(&quot;IN EFFETTI NEL MIO MONDO C'E'&quot; AND A&gt;=.5 AND A&lt;.75)+(&quot;ECCOLO, VEDO PROPRIO&quot; AND A&gt;=.75)'&quot;UN BLOCCO COLORATO DI &quot;;: LET X$=B$(W,1): GO SUB 2690: PRINT : INPUT &quot;VUOI SAPERE DOVE SI TROVA &gt;&quot;;LINE R$
2500 IF R$=&quot;SI&quot; THEN LET C$=X$: GO TO 2360
2510 GO SUB 160: GO TO 1560
2520 REM 
2530 REM **********************
2540 REM * CONTROLLO SINTASSI *
2550 REM **********************
2560 REM 
2570 LET Q=Q+1
2580 IF B$(Q, TO 6)=&quot;BLOCCO&quot; OR B$(Q, TO 4)=&quot;CUBO&quot; OR (B$(Q, TO 6)=&quot;QUELLO&quot; AND FLAG) THEN RETURN 
2590 IF Q&lt; PAROLA THEN GO TO 2570
2600 PRINT #0;AT 0,0;&quot;   NON SO A COSA TU TI STIA&quot;'&quot;  RIFERENDO. SII PIU' CHIARO&quot;: PAUSE 250: GO TO 1560
2610 REM 
2620 REM ********************
2630 REM * CONTROLLO COLORE *
2640 REM ********************
2650 REM 
2660 LET W=W+1: IF B$(W, TO 3)=&quot;BLU&quot; OR B$(W, TO 6)=&quot;GIALLO&quot; OR B$(W, TO 5)=&quot;ROSSO&quot; OR B$(W, TO 5)=&quot;VERDE&quot; THEN RETURN 
2670 IF W&lt; PAROLA THEN GO TO 2660
2680 PRINT #0;AT 0,0;  &quot;NON RIESCO A TROVARE&quot;'&quot;IL CUBO DI CUI HAI PARLATO&quot;: PAUSE 250: GO TO 1560
2690   PRINT X$+(&quot;LU&quot; AND X$=&quot;B&quot;)+(&quot;IALLO&quot; AND X$=&quot;G&quot;)+(&quot;OSSO&quot; AND X$=&quot;R&quot;)+(&quot;ERDE&quot; AND X$=&quot;V&quot;);: RETURN 
2700 REM 
2710 REM *****************
2720 REM * PRESENTAZIONE *
2730 REM *****************
2740 REM 
2750 CLS : POKE 23624,71: PRINT '&quot;   {+4}{-6}{-5}  {+6}{-6}{-4}{-3}{+7}{+6}{-6}{-5} {+5}{+5}&quot;'&quot;   {+4}{-6}{-5}  {+5}{-5}{-5}  {+5} {-5}{+3}{+5}{+5}   {(C)} 1987 by&quot;'&quot;   {+1}{+6}{-5}{+3}{+7}{-6}{+6}{-1}{+3}{-2}{-6}{+6}{-5} {+5}{+5}  ZULIANI  G.&quot;
2760 PRINT ''&quot;CIAO, IO SONO LO SPECTRUM...&quot;: INPUT &quot;COME TI CHIAMI ? &quot;;LINE H$
2770 PRINT '&quot;SALVE, &quot;: LET H$=H$+&quot; !                              &quot;: LET H$=H$( TO 32): FOR F=1 TO 24: PRINT AT 8,31-F;H$(1 TO F): NEXT F: LET F=1
2780 IF H$(F)=&quot;!&quot; THEN LET H$=H$( TO F): GO TO 2800
2790 LET F=F+1: GO TO 2780
2800 PRINT '&quot;LO SCOPO DI QUESTO PROGRAMMA E'&quot;'&quot;DI FARMI APPARIRE INTELLIGENTE!&quot;''&quot;SE AVRAI L'IMPRESSIONE DI AVERE&quot;'&quot;A CHE FARE CON QUALCOSA CHE&quot;'&quot;CAPISCA QUELLO CHE TU GLI DICI,&quot;'&quot;E INOLTRE AGISCA DI CONSEGUENZA,&quot;
2810 PRINT &quot;ALLORA IL SOFTWARE POTRA' ESSERE&quot;'&quot;CONSIDERATO UN  ESEMPIO DI A.I.&quot;'&quot;(ARTIFICIAL INTELLIGENCE)&quot;
2820 PRINT #0;AT 0,0;&quot;* PREMI UN TASTO *&quot;: PAUSE 0
2830 FOR F=6 TO 21: PRINT AT F,0,,: NEXT F: PRINT AT 6,0;
2840 PRINT &quot;PER MEZZO DELLA TASTIERA,&quot;'&quot;UTILIZZANDO FRASI IN ITALIANO,&quot;'&quot;NON ECCESSIVAMENTE COMPLESSE,&quot;'&quot;MI DIRAI QUALI OPERAZIONI DOVRO'&quot;'&quot;COMPIERE SU ALCUNI CUBI COLORATI&quot;'&quot;(SPOSTAMENTI,RICERCHE,ECCETERA).&quot;
2850 PRINT '''&quot;FAMMI UN FAVORE, &quot;;H$
2860 PRINT AT 17,0;&quot;   SE PER CASO GIANFRANCO TI&quot;'&quot;   DOVESSE CHIEDERE UN PARERE,&quot;'&quot;   DIGLI CHE TI SONO SEMBRATO&quot;'&quot;  MOLTO INTELLIGENTE... GRAZIE&quot;
2870 PLOT 14,6: DRAW 0,36: DRAW 228,0: DRAW 0,-36: DRAW -228,0: PLOT 12,4: DRAW 0,40: DRAW 232,0: DRAW 0,-40: DRAW -232,0: PAUSE 0: CLS : RETURN 
</pre>
<p class="modification-notice">Pagina modificata l'8/11/2011</p>
</body>
</html>
